buildscript {
    ext {
        kotlin_version = "1.3.72"
        spigot_version = "1.12.2-R0.1-SNAPSHOT"
        worldguard_version = "6.2"
        item_nbt_api_version = "1.8.2-SNAPSHOT"
        bstats_version = "1.4"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "java"
apply plugin: "kotlin"

group = "click.seichi"
version = "1.1.0"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

configurations {
    embed
    compile.extendsFrom(embed)
}

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    // WorldGuard用
    maven { url = "https://maven.enginehub.org/repo/" }
    // Item-NBT-API用
    maven {
        name = "codemc-repo"
        url = "https://repo.codemc.org/repository/maven-public"
    }
}

dependencies {
    implementation "com.sk89q.worldguard:worldguard-legacy:$worldguard_version"
    implementation "org.spigotmc:spigot-api:$spigot_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // bstatsはItemNBTAPIに要求されたので追加。本来要求されてるのは1.2っぽい
    implementation "org.bstats:bstats-bukkit-lite:$bstats_version"
    implementation "de.tr7zw:item-nbt-api-plugin:$item_nbt_api_version"
    embed "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation fileTree(dir: "libs", include: "*.jar")
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filteringCharset = "UTF-8"
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

